/*
 * example.namidi
 */

title "example"
resolution 480

time 4/4
tempo 120.0


// Guitar
channel 1
sound select 0 0 30 from 0
gatetime cutoff 20
velocity 110

guitar = {
  note from 0 step 120 {
    E1 ~ G E A E G E ~ ~ ~ ~ ^ ^ ^ ^
  }
}

guitar
guitar
guitar
note step 120 {
  E1 ~ G E A E G E 
  ~ ^ E1,A,D,G,B velocity=127 ~ ~ ~ ~ ^
}


// Base
channel 2
sound select 0 0 34 from 0
gatetime cutoff 20
velocity 127

base = {
  note step 120 { E0 ~ G E A E G E ~ ~ ~ ~ ^ ^ ^ ^ }
}

base from 3:1:0
note from 4:1:0 step 120 {
  E0 ~ G E A E G E ~ ^ E ~ ~ ~ ~ ^
}

// Drums
channel 9
sound select 127 0 0 from 0
gatetime 100
velocity 100

drums = {
  note from 0 step 240 { F#1 F#  F#  F#  F#  F#  F#  F# }
  note from 0 step 240 { ^   ^   E   ^   ^   ^   E   ^  }
  note from 0 step 120 { C ^ ^ ^ ^ ^ ^ C C ^ C ^ ^ ^ ^  }
}

drums from m2
drums from m3
drums from m4 {
  replace from b3 {
    note step 120 {
      C ^ ^ ^
    }
  }
}
