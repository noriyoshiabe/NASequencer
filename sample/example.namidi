channel 10
sound select 127 0 0
octave 2
velocity 100
gatetime 80

hihat = {
  note 1/16 3 {
    F# ^ F# A# ^ ^
    F# ^ F# A# ^ ^
    F# ^ F# A# ^ ^
    F# ^ F# A# ^ ^
  }
}

ride = {
  note 1/8 {
    D#> D#> D#> D#> D#> D#> D#> D#>
  }
}

bass_and_snare1 = {
  note 1/16 3 {
    C ^ ^ ^ ^ ^
    E(velocity=127) ^ ^ ^ ^ C
    ^ ^ C C ^ ^ E(velocity=127) ^ ^ ^ ^ ^
  }
}

bass_and_snare2 = bass_and_snare1 {
  mix from 4:0 {
    note 1/16 3 {
      ^ ^ C ^ ^ E(velocity=127)
    }
  }
}

channel 1
sound select 0 0 0
octave 3
gatetime cutoff 20

piano1 = {
  note 1/16 3 {
    A< ^ A< G#,D>,G> ~ ~
    ^  ^ A< G#,D>,G> ~ ~
    ^  ^ A< G#,D>,G> ~ ~
    ^  ^ A,C#>,G> ~~ A,C#>
  }
}
piano2 = {
  note 1/1 {
    D,G,B
  }
}

channel 2
sound select 0 0 33
octave 1
bass1 = {
  note 1/8 {
    E G A A# B A# A G
  }
}
bass2 = {
  note 1/8 {
    E G A A# B E> D> E>
  }
}
octave 2
bass3 = {
  note 1/8 {
    A< C D D# E D# D C
  }
}
bass4 = {
  note 1/8 {
    A< C D D# E A G A
  }
}
bass5 = {
  octave 1
  note 1/1 {
    E
  }
}

hihat, bass_and_snare1
hihat, bass_and_snare2
hihat, bass_and_snare1, bass1
hihat, bass_and_snare2, bass2
hihat, bass_and_snare1, bass3, piano1
hihat, bass_and_snare2, bass4, piano1

ride, bass_and_snare1, bass1, piano2
ride, bass_and_snare2, bass2
ride, bass_and_snare1, bass3, piano1
ride, bass_and_snare2, bass4, piano1

hihat, bass_and_snare1, bass5

last = {
  hihat, bass_and_snare2
}
last {
  replace channel 10 from b3 {
    octave 2
    note 1/32 {
      E E C A G F C E
      E(velocity=127) E
      C ^ C C
      E(velocity=127), A#
      ^ G#
    }
  }
}
